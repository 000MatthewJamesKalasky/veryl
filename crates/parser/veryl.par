
%start Veryl
%title "Veryl grammar"
%comment "Empty grammar generated by `parol`"
%user_type VerylToken = crate::veryl_token::VerylToken
%user_type OwnedToken = crate::veryl_token::OwnedToken

%%

// ----------------------------------------------------------------------------
// Terminals
// ----------------------------------------------------------------------------


MultiComment: "(?:(?:(?://.*(?:\r\n|\r|\n|$))|(?:(?ms)/\u{2a}.*?\u{2a}/))\s*)+": OwnedToken;
Comments    : [ MultiComment ];

// Start
StartToken: Comments;
Start     : StartToken: VerylToken;

// Number
ExponentToken    : /[0-9]+(?:_[0-9]+)*\.[0-9]+(?:_[0-9]+)*[eE][+-]?[0-9]+(?:_[0-9]+)*/: OwnedToken Comments;
FixedPointToken  : /[0-9]+(?:_[0-9]+)*\.[0-9]+(?:_[0-9]+)*/: OwnedToken Comments;
BasedBinaryToken : /[0-9]+(?:_[0-9]+)*'b[0-1xzXZ]+(?:_[0-1xzXZ]+)*/: OwnedToken Comments;
BasedOctalToken  : /[0-9]+(?:_[0-9]+)*'o[0-7xzXZ]+(?:_[0-7xzXZ]+)*/: OwnedToken Comments;
BasedDecimalToken: /[0-9]+(?:_[0-9]+)*'d[0-9]+(?:_[0-9]+)*/: OwnedToken Comments;
BasedHexToken    : /[0-9]+(?:_[0-9]+)*'h[0-9a-fA-FxzXZ]+(?:_[0-9a-fA-FxzXZ]+)*/: OwnedToken Comments;
BaseLessToken    : /[0-9]+(?:_[0-9]+)*/: OwnedToken Comments;
AllBitToken      : /'[01]/: OwnedToken Comments;

Exponent    : ExponentToken    : VerylToken;
FixedPoint  : FixedPointToken  : VerylToken;
BasedBinary : BasedBinaryToken : VerylToken;
BasedOctal  : BasedOctalToken  : VerylToken;
BasedDecimal: BasedDecimalToken: VerylToken;
BasedHex    : BasedHexToken    : VerylToken;
BaseLess    : BaseLessToken    : VerylToken;
AllBit      : AllBitToken      : VerylToken;

// Double Symbol
CircumflexTildeToken: '^~': OwnedToken Comments;
TildeAmpToken       : '~&': OwnedToken Comments;
TildeCircumflexToken: '~^': OwnedToken Comments;
TildeOrToken        : '~|': OwnedToken Comments;

CircumflexTilde: CircumflexTildeToken: VerylToken;
TildeAmp       : TildeAmpToken       : VerylToken;
TildeCircumflex: TildeCircumflexToken: VerylToken;
TildeOr        : TildeOrToken        : VerylToken;

// Single Symbol
AmpToken       : '&': OwnedToken Comments;
BangToken      : '!': OwnedToken Comments;
CircumflexToken: '^': OwnedToken Comments;
ColonToken     : ':': OwnedToken Comments;
CommaToken     : ',': OwnedToken Comments;
EquToken       : '=': OwnedToken Comments;
HashToken      : '#': OwnedToken Comments;
LBraceToken    : '{': OwnedToken Comments;
LBracketToken  : '[': OwnedToken Comments;
LParenToken    : '(': OwnedToken Comments;
MinusToken     : '-': OwnedToken Comments;
OrToken        : '|': OwnedToken Comments;
PercentToken   : '%': OwnedToken Comments;
PlusToken      : '+': OwnedToken Comments;
RBraceToken    : '}': OwnedToken Comments;
RBracketToken  : ']': OwnedToken Comments;
RParenToken    : ')': OwnedToken Comments;
SemicolonToken : ';': OwnedToken Comments;
SlashToken     : '/': OwnedToken Comments;
StarToken      : '*': OwnedToken Comments;
TildeToken     : '~': OwnedToken Comments;

Amp       : AmpToken       : VerylToken;
Bang      : BangToken      : VerylToken;
Circumflex: CircumflexToken: VerylToken;
Colon     : ColonToken     : VerylToken;
Comma     : CommaToken     : VerylToken;
Equ       : EquToken       : VerylToken;
Hash      : HashToken      : VerylToken;
LBrace    : LBraceToken    : VerylToken;
LBracket  : LBracketToken  : VerylToken;
LParen    : LParenToken    : VerylToken;
Minus     : MinusToken     : VerylToken;
Or        : OrToken        : VerylToken;
Percent   : PercentToken   : VerylToken;
Plus      : PlusToken      : VerylToken;
RBrace    : RBraceToken    : VerylToken;
RBracket  : RBracketToken  : VerylToken;
RParen    : RParenToken    : VerylToken;
Semicolon : SemicolonToken : VerylToken;
Slash     : SlashToken     : VerylToken;
Star      : StarToken      : VerylToken;
Tilde     : TildeToken     : VerylToken;

// Keyword
AlwaysCombToken: 'always_comb': OwnedToken Comments;
AlwaysFfToken  : 'always_ff'  : OwnedToken Comments;
AssignToken    : 'assign'     : OwnedToken Comments;
BitToken       : 'bit'        : OwnedToken Comments;
ElseToken      : 'else'       : OwnedToken Comments;
F32Token       : 'f32'        : OwnedToken Comments;
F64Token       : 'f64'        : OwnedToken Comments;
I32Token       : 'i32'        : OwnedToken Comments;
I64Token       : 'i64'        : OwnedToken Comments;
IfToken        : 'if'         : OwnedToken Comments;
InoutToken     : 'inout'      : OwnedToken Comments;
InputToken     : 'input'      : OwnedToken Comments;
InterfaceToken : 'interface'  : OwnedToken Comments;
LocalparamToken: 'localparam' : OwnedToken Comments;
LogicToken     : 'logic'      : OwnedToken Comments;
ModportToken   : 'modport'    : OwnedToken Comments;
ModuleToken    : 'module'     : OwnedToken Comments;
NegedgeToken   : 'negedge'    : OwnedToken Comments;
OutputToken    : 'output'     : OwnedToken Comments;
ParameterToken : 'parameter'  : OwnedToken Comments;
PosedgeToken   : 'posedge'    : OwnedToken Comments;
U32Token       : 'u32'        : OwnedToken Comments;
U64Token       : 'u64'        : OwnedToken Comments;

AlwaysComb: AlwaysCombToken: VerylToken;
AlwaysFf  : AlwaysFfToken  : VerylToken;
Assign    : AssignToken    : VerylToken;
Bit       : BitToken       : VerylToken;
Else      : ElseToken      : VerylToken;
F32       : F32Token       : VerylToken;
F64       : F64Token       : VerylToken;
I32       : I32Token       : VerylToken;
I64       : I64Token       : VerylToken;
If        : IfToken        : VerylToken;
Inout     : InoutToken     : VerylToken;
Input     : InputToken     : VerylToken;
Interface : InterfaceToken : VerylToken;
Localparam: LocalparamToken: VerylToken;
Logic     : LogicToken     : VerylToken;
Modport   : ModportToken   : VerylToken;
Module    : ModuleToken    : VerylToken;
Negedge   : NegedgeToken   : VerylToken;
Output    : OutputToken    : VerylToken;
Parameter : ParameterToken : VerylToken;
Posedge   : PosedgeToken   : VerylToken;
U32       : U32Token       : VerylToken;
U64       : U64Token       : VerylToken;

// Identifier
IdentifierToken: /[a-zA-Z_][0-9a-zA-Z_]*/: OwnedToken Comments;
Identifier     : IdentifierToken: VerylToken;

// ----------------------------------------------------------------------------
// SourceCode
// ----------------------------------------------------------------------------

Veryl: Start { Description };

// ----------------------------------------------------------------------------
// Description
// ----------------------------------------------------------------------------

Description: ModuleDeclaration
           | InterfaceDeclaration
           ;

// ----------------------------------------------------------------------------
// Number
// ----------------------------------------------------------------------------

Number: IntegralNumber
      | RealNumber
      ;

IntegralNumber: BasedBinary
              | BasedOctal
              | BasedDecimal
              | BasedHex
              | BaseLess
              | AllBit
              ;

RealNumber: FixedPoint
          | Exponent
          ;

// ----------------------------------------------------------------------------
// Expression
// ----------------------------------------------------------------------------

OperatorPrecedence1: Plus | Minus;
OperatorPrecedence2: Star | Slash | Percent;
OperatorPrecedence3: Plus | Minus | Bang | Tilde | Amp | Or | Circumflex | TildeAmp | TildeOr | TildeCircumflex | CircumflexTilde;

Expression : Expression0;
Expression0: Expression1 { OperatorPrecedence1 Expression1 };
Expression1: Expression2 { OperatorPrecedence2 Expression2 };
Expression2: [ OperatorPrecedence3 ] Expression3;
Expression3: Factor;

Factor: Number
      | Identifier { Range }
      | LParen Expression RParen
      ;

// ----------------------------------------------------------------------------
// Statement
// ----------------------------------------------------------------------------

Statement: AssignmentStatement
         | IfStatement
         ;

AssignmentStatement: Identifier Equ Expression Semicolon;

IfStatement: If Expression LBrace Statement RBrace { Else If Expression LBrace Statement RBrace } [ Else LBrace Statement RBrace ];

// ----------------------------------------------------------------------------
// Range / Width
// ----------------------------------------------------------------------------

Range: LBracket Expression [ Colon Expression ] RBracket;

Width: LBracket Expression RBracket;

// ----------------------------------------------------------------------------
// Type
// ----------------------------------------------------------------------------

BuiltinType: Logic
           | Bit
           | U32 | U64 | I32 | I64 | F32 | F64
           ;

Type: ( BuiltinType | Identifier ) { Width };

// ----------------------------------------------------------------------------
// WithParameter
// ----------------------------------------------------------------------------

WithParameter: Hash LParen [ WithParameterList ] RParen;

WithParameterList: WithParameterItem { Comma WithParameterItem } [ Comma ];

WithParameterItem: ( Parameter | Localparam ) Identifier Colon Type Equ Expression;

// ----------------------------------------------------------------------------
// Module
// ----------------------------------------------------------------------------

ModuleDeclaration: Module Identifier [ WithParameter ] [ ModulePort ] LBrace { ModuleItem } RBrace;

ModulePort: LParen [ ModulePortList ] RParen;

ModulePortList: ModulePortItem { Comma ModulePortItem } [ Comma ];

ModulePortItem: Identifier Colon Direction Type;

Direction: Input
         | Output
         | Inout
         ;

ModuleItem: VariableDeclaration
          | ParameterDeclaration
          | LocalparamDeclaration
          | AlwaysFfDeclaration
          | AlwaysCombDeclaration
          | AssignDeclaration
          ;

// ----------------------------------------------------------------------------
// Interface
// ----------------------------------------------------------------------------

InterfaceDeclaration: Interface Identifier [ WithParameter ] LBrace { InterfaceItem } RBrace;

InterfaceItem: VariableDeclaration
             | ParameterDeclaration
             | LocalparamDeclaration
             | ModportDeclaration
             ;

// ----------------------------------------------------------------------------
// Declaration
// ----------------------------------------------------------------------------

VariableDeclaration: Identifier Colon Type Semicolon;

ParameterDeclaration: Parameter Identifier Colon Type Equ Expression Semicolon;

LocalparamDeclaration: Localparam Identifier Colon Type Equ Expression Semicolon;

AlwaysFfDeclaration: AlwaysFf LParen AlwaysFfConditions RParen LBrace { Statement } RBrace;

AlwaysFfConditions: AlwaysFfCondition { Comma AlwaysFfCondition } [ Comma ];

AlwaysFfCondition: ( Posedge | Negedge ) Identifier;

AlwaysCombDeclaration: AlwaysComb LBrace { Statement } RBrace;

AssignDeclaration: Assign Identifier [ Colon Type ] Equ Expression Semicolon;

ModportDeclaration: Modport Identifier LBrace ModportList RBrace;

ModportList: ModportItem { Comma ModportItem } [ Comma ];

ModportItem: Identifier Colon Direction;
