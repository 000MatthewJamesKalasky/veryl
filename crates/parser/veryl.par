
%start Veryl
%title "Veryl grammar"
%comment "Empty grammar generated by `parol`"
%user_type VerylToken = crate::veryl_token::VerylToken
%user_type Token = crate::veryl_token::Token

%%

// ----------------------------------------------------------------------------
// Terminals
// ----------------------------------------------------------------------------

MultiComment: "(?:(?:(?://.*(?:\r\n|\r|\n|$))|(?:(?ms)/\u{2a}.*?\u{2a}/))\s*)+": Token;
Comments    : [ MultiComment ];

StartToken: Comments;

ExponentToken    : /[0-9]+(?:_[0-9]+)*\.[0-9]+(?:_[0-9]+)*[eE][+-]?[0-9]+(?:_[0-9]+)*/: Token Comments;
FixedPointToken  : /[0-9]+(?:_[0-9]+)*\.[0-9]+(?:_[0-9]+)*/: Token Comments;
BasedToken       : /[0-9]+(?:_[0-9]+)*'[bodh][0-9a-fA-FxzXZ]+(?:_[0-9a-fA-FxzXZ]+)*/: Token Comments;
BaseLessToken    : /[0-9]+(?:_[0-9]+)*/: Token Comments;
AllBitToken      : /'[01]/: Token Comments;

// Some tokens should be before operator
MinusColonToken: '-:': Token Comments;
MinusGTToken   : '->': Token Comments;
PlusColonToken : '+:': Token Comments;

AssignmentOperatorToken: "\+=|-=|\*=|/=|%=|&=|\|=|\^=|<<=|>>=|<<<=|>>>="                      : Token Comments;
BinaryOperatorToken    : "\*\*|\*|/|%|<<<|>>>|<<|>>|<=|>=|<|>|===|==\?|!==|!=\?|==|!=|&&|\|\|": Token Comments;
CommonOperatorToken    : "\+|-|&|\||\^~|\^|~\^|~&|~\|"                                        : Token Comments;
UnaryOperatorToken     : "!|~"                                                                : Token Comments;

ColonToken    : ':' : Token Comments;
CommaToken    : ',' : Token Comments;
DollarToken   : '$' : Token Comments;
DotDotToken   : '..': Token Comments;
DotToken      : '.' : Token Comments;
EquToken      : '=' : Token Comments;
HashToken     : '#' : Token Comments;
LBraceToken   : '{' : Token Comments;
LBracketToken : '[' : Token Comments;
LParenToken   : '(' : Token Comments;
RBraceToken   : '}' : Token Comments;
RBracketToken : ']' : Token Comments;
RParenToken   : ')' : Token Comments;
SemicolonToken: ';' : Token Comments;

AlwaysCombToken: /\balways_comb\b/: Token Comments;
AlwaysFfToken  : /\balways_ff\b/  : Token Comments;
AssignToken    : /\bassign\b/     : Token Comments;
AsyncHighToken : /\basync_high\b/ : Token Comments;
AsyncLowToken  : /\basync_low\b/  : Token Comments;
BitToken       : /\bbit\b/        : Token Comments;
ElseToken      : /\belse\b/       : Token Comments;
EnumToken      : /\benum\b/       : Token Comments;
F32Token       : /\bf32\b/        : Token Comments;
F64Token       : /\bf64\b/        : Token Comments;
ForToken       : /\bfor\b/        : Token Comments;
FunctionToken  : /\bfunction\b/   : Token Comments;
I32Token       : /\bi32\b/        : Token Comments;
I64Token       : /\bi64\b/        : Token Comments;
IfResetToken   : /\bif_reset\b/   : Token Comments;
IfToken        : /\bif\b/         : Token Comments;
InoutToken     : /\binout\b/      : Token Comments;
InputToken     : /\binput\b/      : Token Comments;
InstToken      : /\binst\b/       : Token Comments;
InterfaceToken : /\binterface\b/  : Token Comments;
InToken        : /\bin\b/         : Token Comments;
LetToken       : /\blet\b/        : Token Comments;
LocalparamToken: /\blocalparam\b/ : Token Comments;
LogicToken     : /\blogic\b/      : Token Comments;
ModportToken   : /\bmodport\b/    : Token Comments;
ModuleToken    : /\bmodule\b/     : Token Comments;
NegedgeToken   : /\bnegedge\b/    : Token Comments;
OutputToken    : /\boutput\b/     : Token Comments;
ParameterToken : /\bparameter\b/  : Token Comments;
PosedgeToken   : /\bposedge\b/    : Token Comments;
RefToken       : /\bref\b/        : Token Comments;
ReturnToken    : /\breturn\b/     : Token Comments;
StepToken      : /\bstep\b/       : Token Comments;
StructToken    : /\bstruct\b/     : Token Comments;
SyncHighToken  : /\bsync_high\b/  : Token Comments;
SyncLowToken   : /\bsync_low\b/   : Token Comments;
U32Token       : /\bu32\b/        : Token Comments;
U64Token       : /\bu64\b/        : Token Comments;

IdentifierToken: /[a-zA-Z_][0-9a-zA-Z_]*/: Token Comments;

// ----------------------------------------------------------------------------
// VerylToken
// ----------------------------------------------------------------------------

// Start
Start: StartToken: VerylToken;

// Number
Exponent  : ExponentToken  : VerylToken;
FixedPoint: FixedPointToken: VerylToken;
Based     : BasedToken     : VerylToken;
BaseLess  : BaseLessToken  : VerylToken;
AllBit    : AllBitToken    : VerylToken;

// Operator
AssignmentOperator: AssignmentOperatorToken: VerylToken;
CommonOperator    : CommonOperatorToken    : VerylToken;
BinaryOperator    : BinaryOperatorToken    : VerylToken;
UnaryOperator     : UnaryOperatorToken     : VerylToken;

// Symbol
Colon     : ColonToken     : VerylToken;
Comma     : CommaToken     : VerylToken;
Dollar    : DollarToken    : VerylToken;
DotDot    : DotDotToken    : VerylToken;
Dot       : DotToken       : VerylToken;
Equ       : EquToken       : VerylToken;
Hash      : HashToken      : VerylToken;
LBrace    : LBraceToken    : VerylToken;
LBracket  : LBracketToken  : VerylToken;
LParen    : LParenToken    : VerylToken;
MinusColon: MinusColonToken: VerylToken;
MinusGT   : MinusGTToken   : VerylToken;
PlusColon : PlusColonToken : VerylToken;
RBrace    : RBraceToken    : VerylToken;
RBracket  : RBracketToken  : VerylToken;
RParen    : RParenToken    : VerylToken;
Semicolon : SemicolonToken : VerylToken;

// Keyword
AlwaysComb: AlwaysCombToken: VerylToken;
AlwaysFf  : AlwaysFfToken  : VerylToken;
Assign    : AssignToken    : VerylToken;
AsyncHigh : AsyncHighToken : VerylToken;
AsyncLow  : AsyncLowToken  : VerylToken;
Bit       : BitToken       : VerylToken;
Else      : ElseToken      : VerylToken;
Enum      : EnumToken      : VerylToken;
F32       : F32Token       : VerylToken;
F64       : F64Token       : VerylToken;
For       : ForToken       : VerylToken;
Function  : FunctionToken  : VerylToken;
I32       : I32Token       : VerylToken;
I64       : I64Token       : VerylToken;
If        : IfToken        : VerylToken;
IfReset   : IfResetToken   : VerylToken;
In        : InToken        : VerylToken;
Inout     : InoutToken     : VerylToken;
Input     : InputToken     : VerylToken;
Inst      : InstToken      : VerylToken;
Interface : InterfaceToken : VerylToken;
Let       : LetToken       : VerylToken;
Localparam: LocalparamToken: VerylToken;
Logic     : LogicToken     : VerylToken;
Modport   : ModportToken   : VerylToken;
Module    : ModuleToken    : VerylToken;
Negedge   : NegedgeToken   : VerylToken;
Output    : OutputToken    : VerylToken;
Parameter : ParameterToken : VerylToken;
Posedge   : PosedgeToken   : VerylToken;
Ref       : RefToken       : VerylToken;
Return    : ReturnToken    : VerylToken;
Step      : StepToken      : VerylToken;
Struct    : StructToken    : VerylToken;
SyncHigh  : SyncHighToken  : VerylToken;
SyncLow   : SyncLowToken   : VerylToken;
U32       : U32Token       : VerylToken;
U64       : U64Token       : VerylToken;

// Identifier
Identifier: IdentifierToken: VerylToken;

// ----------------------------------------------------------------------------
// Number
// ----------------------------------------------------------------------------

Number: IntegralNumber
      | RealNumber
      ;

IntegralNumber: Based
              | BaseLess
              | AllBit
              ;

RealNumber: FixedPoint
          | Exponent
          ;

// ----------------------------------------------------------------------------
// Hierarchical Identifier
// ----------------------------------------------------------------------------

HierarchicalIdentifier: Identifier { Range } { Dot Identifier { Range } };

// ----------------------------------------------------------------------------
// Expression
// ----------------------------------------------------------------------------

Expression : Expression1 { ( BinaryOperator | CommonOperator ) Expression1 };
Expression1: [ ( UnaryOperator | CommonOperator ) ] Factor;

Factor: Number
      | [ Dollar ] HierarchicalIdentifier [ LParen [ FunctionCallArg ] RParen ]
      | LParen Expression RParen
      ;

FunctionCallArg: Expression { Comma Expression } [ Comma ];

// ----------------------------------------------------------------------------
// Range / Width
// ----------------------------------------------------------------------------

Range: LBracket Expression [ RangeOperator Expression ] RBracket;

RangeOperator: Colon
             | PlusColon
             | MinusColon
             | Step
             ;

Width: LBracket Expression RBracket;

// ----------------------------------------------------------------------------
// Type
// ----------------------------------------------------------------------------

BuiltinType: Logic
           | Bit
           | U32 | U64 | I32 | I64 | F32 | F64
           ;

Type: ( BuiltinType | Identifier ) { Width };

// ----------------------------------------------------------------------------
// Statement
// ----------------------------------------------------------------------------

Statement: AssignmentStatement
         | IfStatement
         | IfResetStatement
         | ReturnStatement
         | ForStatement
         ;

AssignmentStatement: HierarchicalIdentifier ( Equ | AssignmentOperator ) Expression Semicolon;

IfStatement: If Expression LBrace { Statement } RBrace { Else If Expression LBrace { Statement } RBrace } [ Else LBrace { Statement } RBrace ];

IfResetStatement: IfReset LBrace { Statement } RBrace { Else If Expression LBrace { Statement } RBrace } [ Else LBrace { Statement } RBrace ];

ReturnStatement: Return Expression Semicolon;

ForStatement: For Identifier Colon Type In Expression DotDot Expression [ Step AssignmentOperator Expression ] LBrace { Statement } RBrace;

// ----------------------------------------------------------------------------
// Declaration
// ----------------------------------------------------------------------------

LetDeclaration: Let Identifier Colon ( VariableDeclaration | InstanceDeclaration ) Semicolon;

VariableDeclaration: Type [ Equ Expression ];

ParameterDeclaration: Parameter Identifier Colon Type Equ Expression Semicolon;

LocalparamDeclaration: Localparam Identifier Colon Type Equ Expression Semicolon;

AlwaysFfDeclaration: AlwaysFf LParen AlwaysFfClock [ Comma AlwaysFfReset ] RParen LBrace { Statement } RBrace;

AlwaysFfClock: [ Posedge | Negedge ] HierarchicalIdentifier;

AlwaysFfReset: [ AsyncLow | AsyncHigh | SyncLow | SyncHigh ] HierarchicalIdentifier;

AlwaysCombDeclaration: AlwaysComb LBrace { Statement } RBrace;

AssignDeclaration: Assign HierarchicalIdentifier Equ Expression Semicolon;

ModportDeclaration: Modport Identifier LBrace ModportList RBrace;

ModportList: ModportItem { Comma ModportItem } [ Comma ];

ModportItem: Identifier Colon Direction;

EnumDeclaration: Enum Identifier Colon Type LBrace EnumList RBrace;

EnumList: EnumItem { Comma EnumItem } [ Comma ];

EnumItem: Identifier [ Equ Expression ];

StructDeclaration: Struct Identifier LBrace StructList RBrace;

StructList: StructItem { Comma StructItem } [ Comma ];

StructItem: Identifier Colon Type;

// ----------------------------------------------------------------------------
// InstanceDeclaration
// ----------------------------------------------------------------------------

InstanceDeclaration: Inst Identifier [ Width ] [ InstanceParameter ] [ LBrace [ InstancePortList ] RBrace ];

InstanceParameter: Hash LParen [ InstanceParameterList ] RParen;

InstanceParameterList: InstanceParameterItem { Comma InstanceParameterItem } [ Comma ];

InstanceParameterItem: Identifier [ Colon Expression ];

InstancePortList: InstancePortItem { Comma InstancePortItem } [ Comma ];

InstancePortItem: Identifier [ Colon Expression ];

// ----------------------------------------------------------------------------
// WithParameter
// ----------------------------------------------------------------------------

WithParameter: Hash LParen [ WithParameterList ] RParen;

WithParameterList: WithParameterItem { Comma WithParameterItem } [ Comma ];

WithParameterItem: ( Parameter | Localparam ) Identifier Colon Type Equ Expression;

// ----------------------------------------------------------------------------
// PortDeclaration
// ----------------------------------------------------------------------------

PortDeclaration: LParen [ PortDeclarationList ] RParen;

PortDeclarationList: PortDeclarationItem { Comma PortDeclarationItem } [ Comma ];

PortDeclarationItem: Identifier Colon Direction Type;

Direction: Input
         | Output
         | Inout
         | Ref
         ;

// ----------------------------------------------------------------------------
// Function
// ----------------------------------------------------------------------------

FunctionDeclaration: Function Identifier [ WithParameter ] [ PortDeclaration ] MinusGT Type LBrace { FunctionItem } RBrace;

FunctionItem: LetDeclaration
            | Statement
            ;

// ----------------------------------------------------------------------------
// Module
// ----------------------------------------------------------------------------

ModuleDeclaration: Module Identifier [ WithParameter ] [ PortDeclaration ] LBrace { ModuleItem } RBrace;

ModuleIfDeclaration: If Expression Colon Identifier LBrace { ModuleItem } RBrace { Else If Expression [ Colon Identifier ] LBrace { ModuleItem } RBrace } [ Else [ Colon Identifier ] LBrace { ModuleItem } RBrace ];

ModuleForDeclaration: For Identifier In Expression DotDot Expression [ Step AssignmentOperator Expression ] Colon Identifier LBrace { ModuleItem } RBrace;

ModuleItem: LetDeclaration
          | ParameterDeclaration
          | LocalparamDeclaration
          | AlwaysFfDeclaration
          | AlwaysCombDeclaration
          | AssignDeclaration
          | FunctionDeclaration
          | ModuleIfDeclaration
          | ModuleForDeclaration
          | EnumDeclaration
          | StructDeclaration
          ;

// ----------------------------------------------------------------------------
// Interface
// ----------------------------------------------------------------------------

InterfaceDeclaration: Interface Identifier [ WithParameter ] LBrace { InterfaceItem } RBrace;

InterfaceIfDeclaration: If Expression Colon Identifier LBrace { InterfaceItem } RBrace { Else If Expression [ Colon Identifier ] LBrace { InterfaceItem } RBrace } [ Else [ Colon Identifier ] LBrace { InterfaceItem } RBrace ];

InterfaceForDeclaration: For Identifier In Expression DotDot Expression [ Step AssignmentOperator Expression ] Colon Identifier LBrace { InterfaceItem } RBrace;

InterfaceItem: LetDeclaration
             | ParameterDeclaration
             | LocalparamDeclaration
             | ModportDeclaration
             | InterfaceIfDeclaration
             | InterfaceForDeclaration
             | EnumDeclaration
             | StructDeclaration
             ;

// ----------------------------------------------------------------------------
// Description
// ----------------------------------------------------------------------------

Description: ModuleDeclaration
           | InterfaceDeclaration
           ;

// ----------------------------------------------------------------------------
// SourceCode
// ----------------------------------------------------------------------------

Veryl: Start { Description };
